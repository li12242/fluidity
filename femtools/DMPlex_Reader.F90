!    Copyright (C) 2006 Imperial College London and others.
!
!    Please see the AUTHORS file in the main source directory for a full list
!    of copyright holders.
!
!    Prof. C Pain
!    Applied Modelling and Computation Group
!    Department of Earth Science and Engineering
!    Imperial College London
!
!    amcgsoftware@imperial.ac.uk
!
!    This library is free software; you can redistribute it and/or
!    modify it under the terms of the GNU Lesser General Public
!    License as published by the Free Software Foundation; either
!    version 2.1 of the License, or (at your option) any later version.
!
!    This library is distributed in the hope that it will be useful,
!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
!    Lesser General Public License for more details.
!
!    You should have received a copy of the GNU Lesser General Public
!    License along with this library; if not, write to the Free Software
!    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307
!    USA

#include "fdebug.h"
#include "confdefs.h"

module dmplex_reader
  use fields
  use global_parameters, only:FIELD_NAME_LEN
  use parallel_tools, only: isparallel
#ifdef HAVE_PETSC_MODULES
  use petsc
#endif

  implicit none

#include "petsc_legacy.h"

  private

  public :: dmplex_read_exodusii_file

contains

  function dmplex_read_exodusii_file(filename, quad_degree, quad_ngi, quad_family) &
       result (field)
    character(len=*), intent(in) :: filename
    integer, intent(in), optional, target :: quad_degree
    integer, intent(in), optional, target :: quad_ngi
    integer, intent(in), optional :: quad_family
    type(vector_field) :: field

    character(len=FIELD_NAME_LEN) :: partitioner
    DM :: plex, plex_parallel
    PetscSF :: parallelSF
    PetscErrorCode :: ierr

    ewrite(2,*) "In dmplex_read_exodusii_file"

    ! Create a DMPlex object for the ExodusII mesh
    call DMPlexCreateExodusFromFile(MPI_COMM_FEMTOOLS, filename, PETSC_TRUE, plex, ierr)
    if (debug_level() == 2) then
       ewrite(2,*) "Sequential DMPlex derived from ExodusII mesh:"
       call DMView(plex, PETSC_VIEWER_STDOUT_WORLD, ierr)
    end if

    ! Distribute the DMPlex to all ranks in parallel
    if (isparallel()) then
       partitioner = "chaco"
       call DMPlexDistribute(plex, trim(partitioner), 1, parallelSF, plex_parallel, ierr)
       call DMDestroy(plex, ierr)
       plex = plex_parallel
       if (debug_level() == 2) then
          ewrite(2,*) "Distributed DMPlex derived from ExodusII mesh:"
          call DMView(plex, PETSC_VIEWER_STDOUT_WORLD, ierr)
       end if
    end if

    ! Build Coordinate field from derived DMPlex
    field = dmplex_create_coordinate_field(plex, quad_degree=quad_degree, &
         quad_ngi=quad_ngi, quad_family=quad_family)

    call DMDestroy(plex, ierr)

    ewrite(2,*) "Finished dmplex_read_exodusii_file"
    FLExit("DMPlex-ExodusII reader not yet implemented")
  end function dmplex_read_exodusii_file

  function dmplex_create_coordinate_field(plex, quad_degree, quad_ngi, quad_family) &
       result (field)
    DM, intent(in) :: plex
    integer, intent(in), optional, target :: quad_degree
    integer, intent(in), optional, target :: quad_ngi
    integer, intent(in), optional :: quad_family
    type(vector_field) :: field

    type(mesh_type) :: mesh
    type(quadrature_type) :: quad
    type(element_type) :: shape

    PetscInt :: dim, loc, cStart, cEnd, vStart, vEnd, nnodes, nelements
    PetscInt, dimension(:), pointer :: closure
    PetscScalar, dimension(:), pointer :: coordinates => null()
    Vec :: plex_coordinates
    PetscErrorCode :: ierr

    ewrite(2,*) "In dmplex_create_coordinate_field"

    call DMGetDimension(plex, dim, ierr)
    call DMPlexGetDepthStratum(plex, 0, vStart, vEnd, ierr)
    nnodes = vEnd - vStart
    call DMPlexGetHeightStratum(plex, 0, cStart, cEnd, ierr)
    nelements = cEnd - cStart
    ! Assumes no. faces == no. vertices in each element
    call DMPlexGetConeSize(plex, cStart, loc, ierr)

    ! Build mesh shape and quadrature
    if (present(quad_degree)) then
       quad = make_quadrature(loc, dim, degree=quad_degree, family=quad_family)
    else if (present(quad_ngi)) then
       quad = make_quadrature(loc, dim, ngi=quad_ngi, family=quad_family)
    else
       FLAbort("Need to specify either quadrature degree or ngi")
    end if
    shape=make_element_shape(loc, dim, 1, quad)

    ! Allocate Coordinate field and the CoordinateMesh
    call allocate(mesh, nnodes, nelements, shape, name="CoordinateMesh")
    call allocate(field, dim, mesh, name="Coordinate")

    ! Copy DMPlex coordinates to the coordinate field
    call DMGetCoordinates(plex, plex_coordinates, ierr)
    call VecGetArrayF90(plex_coordinates, coordinates, ierr)
    field%val = reshape(coordinates, (/dim, nnodes/))
    call VecRestoreArrayF90(plex_coordinates, coordinates, ierr)

    ! Clean up
    call deallocate_element(shape)
    call deallocate(quad)

    ewrite(2,*) "Finished dmplex_create_coordinate_field"
  end function dmplex_create_coordinate_field

end module dmplex_reader
